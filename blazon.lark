start: arms

arms: field | field "," _charge_group | field _charge_group | _charge_group
field: tincture 
_charge_group:  _charge_group AND charge 
                | _charge_group "between" _charge_group 
                | "on" charge charge 
                | charge+

tincture: METAL| COLOUR | STAIN | fur | variation
METAL: "Or"i
    | "Argent"i 

COLOUR: "Azure"i      // Blue
        | "Gules"i    // Red
        | "Purpure"i  // Purple
        | "Sable"i    // Black
        | "Vert"i     // Green

STAIN:   "Sanguine"i | "Murrey"i // Dark Red
        | "Tenné"i | "Tenny"i    // Tan/orange

fur: ERMINE | VAIR
ERMINE: "Ermine"i // Stoat Fur design (black and white spots). 4 variations
    | "Ermines"i
    | "Erminois"i
    | "Pean"i

VAIR: "Vair"i      // Blue and White alternating bell-shaped pattern
    | "Plumeté"i | "Plumetty"i  // Feather shapes alternating
    | "Papelonné"i | "Papellony"i // Fan/scale shapes alternating
    | "Potent"i    // Tetris T's alternating
    | "Kürsch"i    // Fuzzy squirrel bellies (I am not making this up)

// Variations of the field provide a patterned background. Usually two colors
variation: multipattern tincture AND tincture | multipattern
multipattern: PATTERN "of" number 
             | PATTERN 
             | "Semy" charge        // repeating background image, like a myspace page
PATTERN.2: "Barry"i       // Horizontal stripes.  .2 to avoid conflict Bendy/Bend conflict
        | "Paly"i       // Vertical stripes
        | "Bendy"i      // Diagonal stripes
        | "Chequy"i     // Checkerboard pattern
        | "Checky"i     // Checkerboard pattern
        | "Lozengy"i    // Variations on diagonal checkerboard pattern
        | "Fusilly"i    // Variations on diagonal checkerboard pattern
        | "Masculy"i    // Variations on diagonal checkerboard pattern
        | "Rustré"i     // Variations on diagonal checkerboard pattern
        | "Gyronny"i    // Cool wedge pattern
        | "Quarterly"i  // Divided into 4





charge: number? aspect? ordinary "s"? tincture? 
    | number? aspect? animal tincture?
    | number? aspect? object "s"? tincture?
    | charge "in" ordinary 
    // | charge "and" charge 

animal : beast "s"? attitude? | bird "s"? attitude?

beast:  "Lion"i | "Wolf"i | "Buck"i | "Mule"i | "Leopard"i | "Calf"i | "Calves"i
bird: "Eagle"i | "Martlet"i | "Swan"i | "Raven"i
attitude: "Rampant"i // Standing on it's hind legs!
        | "Passant"i // Walking, one upraised arm
        | "Couchant"i // Sitting like a sphinx


aspect: "Crowned"i       // Can't find the offical heraldic name for these subdescriptions of a charge


ordinary: (HONOURABLE_ORDINARY | SUBORDINARY | DIMINUTIVE) "s"? DIRECTION?
HONOURABLE_ORDINARY:  "Bend"i                   // Diagonal Stripe
                    | "Chevron"i |"Cheveron"i   // Check mark
                    | "Chief"i                  // Stripe on top
                    | "Fesse"i | "Fess"i        // Horizontal Stripe
                    | "Pale"i                   // Vertical Stripe
                    | "Saltire"i                // Giant X
                    | "Cross"i                  // Cross

SUBORDINARY: "Quarter"i 
            | "Bar"i        // Thin horizontal stripe 
DIMINUTIVE:  "pallet"

DIRECTION: "sinister"i | "dexter"i

object: SHAPE | BUILDING | GEOGRAPHY | PLANT | OBJECT
SHAPE: "Escarbuncle"i            // radiating star shape
        | "Fasces"i              // bound rods, mayoral symbol. Root of "Fascism"
        | "Key"i                 // Skeleton Key - represents St. Peter and the Church
BUILDING: "Tower"i
GEOGRAPHY: "Mount"i             // Stylized Hill
PLANT:   "Pomegranate"i             
       | "Rose"i            
       | "Trefoil"i            // 3 4 and 5 pointed flowers  
       | "Quatrefoil"i     
       | "Cinquefoil"i       
       | "Fleurs-de-lys"i    // French - Stylized Lily. Boy Scout Symbol
       | "Fleur-de-lis"i  
       | "Thistle"i  
OBJECT:  "Crown"i
        | "Escallop"i          // Scallop Shell - represents the Santiago de Compostela pilgrimage 
        | "Bell"i
        | "Harp"i
        | "Trumpet"i
        | "Escutcheon"i         // A shield - for recursive shielding

AND.2: "And"i               // The 2 indicates it's higher priority to avoid colision with "an"
number: INT | NUMBER_WORD
NUMBER_WORD: "an"i
        | "a"i
        | "one"i
        | "two"i
        | "three"i
        | "four"i
        | "five"i
        | "six"i
        | "seven"i
        | "eight"i
        | "nine"i
        | "ten"i


%import common.INT 
WHITESPACE: (" " | "\n")+
%ignore WHITESPACE

